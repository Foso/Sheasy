buildscript {
    apply from: './constants.gradle'


}

plugins {
    id("com.android.application")
    id 'org.jetbrains.kotlin.multiplatform'
    id("kotlin-kapt")
    id("androidx.navigation.safeargs")
    id("kotlin-android-extensions")
    id("base")
    id("changelog")
    id("io.gitlab.arturbosch.detekt")

}

android {


    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'

        exclude 'META-INF/INDEX.LIST'
        exclude 'META-INF/io.netty.versions.properties'

    }
    compileSdkVersion configuration.compileSdk
    defaultConfig {
        buildConfigField "int", "SERVER_PORT", configuration.serverPort
        buildConfigField "int", "WEBSOCKET_PORT", configuration.websocketPort
        buildConfigField "String", "GIT_SHA", "\"${gitSha}\""
        buildConfigField "String", "BUILD_TIME", "\"${buildTime}\""
        buildConfigField "String", "APP_VERSION", "\"${configuration.appVersion}\""


        applicationId configuration.applicationId
        minSdkVersion configuration.minSdk
        targetSdkVersion configuration.targetSdk
        versionCode 1
        versionName configuration.appVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            testProguardFile 'tests-proguard.cfg'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8

    }
    androidExtensions {
        experimental = true
    }

    flavorDimensions 'static'


    productFlavors {
        mock {
            dimension 'static'
            applicationIdSuffix = ".mock"
        }
        prod {
            dimension 'static'
        }
    }

   sourceSets{


       mock {
           manifest.srcFile 'src/main/AndroidManifest.xml'
           java.srcDirs = ['src/mock/java']
           res.srcDirs = ['src/main/res']
           assets.srcDirs = ['src/main/assets']
       }

       prod {
           manifest.srcFile 'src/main/AndroidManifest.xml'
           java.srcDirs = ['src/prod/java']
           res.srcDirs = ['src/main/res']
           assets.srcDirs = ['src/main/assets']
       }
   }

    // Always show the result of every unit test, even if it passes.
    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }

    signingConfigs {
        debug {
            storeFile file('../keystore/debug.keystore.jks')
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }
}


detekt {
    toolVersion = "1.0.0.RC8"
    debug = true
    input = files("src/main/java")
    filters = ".*/resources/.*,.*/build/.*"
    config = files("$projectDir/default-detekt-config.yml")
    reports {
        xml {
            enabled = true
            destination = file("$projectDir/output.xml")
        }
        html {
            enabled = true
            destination = file("$projectDir/output.xml")
        }
    }
}


kotlin {
    targets {
        fromPreset(presets.android, 'main') {}
        fromPreset(presets.jvm, 'jvm') {
            configure([compilations.main]) {
                tasks.getByName(compileKotlinTaskName).kotlinOptions {
                    jvmTarget = "1.8"
                }
            }
        }
    }
}


dependencies {
    implementation project(":libshared")

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    //AndroidX
    implementation "androidx.annotation:annotation:$libs.androidX.annotation"
    implementation "androidx.appcompat:appcompat:$libs.androidX.appcompat"
    implementation "androidx.cardview:cardview:$libs.androidX.cardview"
    implementation "androidx.constraintlayout:constraintlayout:${libs.androidX.constraintLayout}"
    implementation "androidx.core:core-ktx:$libs.ktx"
    implementation "androidx.fragment:fragment-ktx:$libs.androidX.fragment"
    implementation "androidx.recyclerview:recyclerview:${libs.androidX.general}"
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.lifecycle:lifecycle-reactivestreams:2.0.0'

    implementation 'com.google.android.material:material:1.0.0'

    //NavigationLib
    implementation "android.arch.navigation:navigation-fragment-ktx:$libs.androidX.nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$libs.nav_version"
    implementation 'com.shopify:livedata-ktx:2.0.1'

    //Moshi
    implementation "com.serjltt.moshi:moshi-lazy-adapters:${libs.moshilazyadapters}"
    implementation "com.squareup.moshi:moshi-kotlin:${libs.moshikotlin}"

    //Ktor
    implementation "io.ktor:ktor-server-netty:$libs.ktor_version"
    implementation "io.ktor:ktor-websockets:$libs.ktor_version"
    implementation "io.ktor:ktor:$libs.ktor_version"
    implementation "io.ktor:ktor-network-tls:$libs.ktor_version"
    implementation "io.ktor:ktor-gson:$libs.ktor_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-rx2:1.0.1'

    //NanoHTTPD
    implementation "org.nanohttpd:nanohttpd-websocket:$libs.nanohttpdwebsocket"
    implementation 'org.nanohttpd:nanohttpd:2.3.1'
    //RxJava
    implementation "io.reactivex.rxjava2:rxandroid:${libs.rxandroid}"
    implementation "io.reactivex.rxjava2:rxjava:${libs.rxJava}"
    implementation "com.jakewharton.rxbinding2:rxbinding:${libs.rxbinding}"
    implementation "io.reactivex.rxjava2:rxkotlin:${libs.rxkotlin}"

    //Dagger
    implementation "com.google.dagger:dagger-android:${libs.dagger}"
    implementation "com.google.dagger:dagger-android-support:${libs.dagger}"

    // if you use the support libraries
    kapt "com.google.dagger:dagger-compiler:${libs.dagger}"
    kapt "com.google.dagger:dagger-android-processor:${libs.dagger}"

    //Other
    implementation("com.mikepenz:aboutlibraries:${libs.aboutlibraries}") {
        transitive = true
    }
    implementation "com.jakewharton.threetenabp:threetenabp:$libs.threetenabp"
    implementation 'co.zsmb:materialdrawer-kt:2.0.1'

    // optional - Test helpers

    //Testing
    testImplementation group: 'org.mockito', name: 'mockito-core', version: "$libs.mockitoVersion"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testImplementation "org.jetbrains.kotlin:kotlin-test-junit:${configuration.kotlin_version}"
    androidTestImplementation 'androidx.test:runner:1.1.1'
    testImplementation "junit:junit:${libs.junit}"
    androidTestImplementation "android.arch.navigation:navigation-testing:$libs.nav_version"

}



task copyFiles(type: Copy) {
    from '../documentation/API/1'
    into '../documentation/API/2'
}


